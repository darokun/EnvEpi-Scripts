**********************************************************************
* Project:      Advanced Epi 2015
* Program:      AdvEPI_ex_nonlinear_2015_student.sas
* Created:      01.02.2016
* Data set(s):  nonlinear
**********************************************************************;

/***************************************************************************/
/* Start of exercise */
/***************************************************************************/
*libname class 'N:\INSTITUT\Vorlesungen\2015\Advanced EPI I\Day08_Nonlinear\Day08_exercise';
libname class 'D:\Vorlesung_2015\Nonlinear\2015\Exercise';

/*1a: Create new class dataset*/
data nonlinear;	
set class.nonlinear;	
run;

/*1b: Look at the data: What is the number of observations? */





/*2: Scatterplots for atalter alhdlaz atsysmm atdiamm atbmi*/
proc gplot data=nonlinear;
plot atdiamm*atsysmm;
run;
quit;







/*2b: Scatterplots - alternatives*/
ods graphics on;
proc kde data=nonlinear;
  bivar atalter atsysmm / /*bwm=.5*/ plots=contour;
run;

proc kde data=nonlinear;
  bivar atalter atsysmm / /*bwm=.5*/ plots=surface;
run;

ods graphics off;


/*3a: Linear regression for HDL-cholesterol and BMI */

proc genmod data=nonlinear;
model alhdlaz=atbmi / dist=normal; * dist=normal => normal distribution;
output out=b p=yhat;  /*out= defines the output data set, p (predicted) defines the variable name for the predicted values;*/
run;

/*Alternative modelling*/
proc reg data=nonlinear;
model alhdlaz=atbmi;
output out=b p=yhat;  
run;
quit;


/* 3b: Plot a scatterplot for HDL-cholesterol and BMI and add regression line */

symbol1 color=black			/* symbol is a global statement that defines the */
        interpol=none		/* characteristics of symbols that display the data plotted */
		value=dot;			/* by proc gplot */
symbol2 color=blue			/* value speficies a plot symbol for the data points */
        interpol=join;		/* color specifies the color for the symbols*/
							/* interpol specifies whether the symbols are interpolated or not */

proc gplot data=b;
plot (alhdlaz yhat)*atbmi/ overlay; 	/* overlay places all the plots that are generated by */
run;										/* the PLOT statement on one set of axes */
quit;

symbol1;	/* resets symbol1 to default SAS characteristics  */
symbol2;



/*4: Polynomial regression for HDL-cholesterol and BMI */

*4a: Create squared variable bmi2 and perform regression with quadratic polynomial;
data nonlinear;
set nonlinear;
atbmi2=atbmi*atbmi;
run;

proc genmod data=nonlinear;
model alhdlaz=atbmi atbmi2 / dist=normal;
output out=b p=yhat;
run;

proc reg data=nonlinear;
model alhdlaz=atbmi atbmi2 ;
output out=b p=yhat;
run;
quit;

*Plot the quadratic polynomial; 
symbol1 color=black			
        interpol=none		
		value=dot;		
symbol2 color=blue
		interpol=splines;			/* spline specifies that the interpolation for the plot line use a spline routine*/ 			
proc gplot data=b;
plot (alhdlaz yhat)*atbmi/ overlay;
run;
quit;


*4c: perform regression with different degrees for the polynomial;
/*First add polynomial terms to your dataset, e.g. for degree=3  */
data nonlinear;
set nonlinear;
atbmi3=atbmi*atbmi*atbmi; /* easier: atbmi**3 */
run;

/*degree=3*/




/*other degrees*/







/*5a: Perform piecewise linear regression model for alhdlaz and bmi*/

data nonlinear;
set nonlinear;
if atbmi<30 then bmic=0; else bmic=1;
bmidiff=(atbmi-30)*bmic;
run;

proc genmod data=nonlinear;
model alhdlaz=atbmi bmidiff/ dist=normal;
output out=b p=yhat;
run;
quit;

/*5b: Plot results*/
symbol1 color=black			
        interpol=none		
		value=dot;		
symbol2 color=blue
		interpol=splines width=2;
proc gplot data=b;
plot alhdlaz*atbmi yhat*atbmi/ overlay;
run;
quit;



/*6a: Regression with polynomial splines B-spline*/

proc transreg data = nonlinear design;
	model identity(alhdlaz) = bspline(atbmi/degree=3  knots=30)/ SS2;
	output out=splineout;
	run;

/*6b: Plot basis functions */
proc sort data=splineout; by atbmi; run; /*Needs to be sorted by x if dots should be joined - try out without the sorting and withdots instead of join*/

goptions reset=all;
goptions colors=(darkred orange darkgreen black blue) ;
symbol interpol=join width=2 repeat=5	;		

proc gplot data=splineout;
plot (Intercept -- atbmi_4)*atbmi/ overlay;
run;
quit;


/*6c: Add weighted basis functions to data set*/
data splineout;
		set splineout;
		t0 = 71.8235080*atbmi_0;
		t1 = 65.9769697*atbmi_1;
		t2 = 32.0049391*atbmi_2;
		t3 = 59.1401642*atbmi_3;
		t4 = 35.7445817*atbmi_4;
		run;


/*6d: Plot weighted basis functions*/






/*6e: Calculate prediction by summing up the weighted basis functions*/









/*Alternative and much easier way*/
proc sort data= nonlinear; by atbmi ; run;

proc transreg data = nonlinear design;
	model identity(alhdlaz) = bspline(atbmi/degree=3  knots=30)/ SS2;
	output out=splineout PREDICTED;  /*Specification of PREDICTED adds the predicted values to the output data set -> default name is Py -> here Palhdlaz */
	run;



symbol1 color=black			
        interpol=none		
		value=dot;		
symbol2 color=blue
		interpol=splines;

proc gplot data=splineout;
plot (alhdlaz Palhdlaz)*atbmi/ overlay;
run;
quit;



/*7: Bin smoother for BMI */

/*7: 	Define categories according to WHO definition*/

data nonlinear;
set nonlinear;
if .< atbmi<25 then bmicat=22;
if atbmi ge 25 and atbmi<30 then bmicat=27.5;
if atbmi ge 30 then bmicat=33;
if atbmi=.  then bmicat=10;
run;

/*Plot boxplots*/
symbol1 color=black			
        interpol=none		
		value=dot;
symbol2 color=blue
		interpol=BOX width=2;
proc gplot data=nonlinear;
plot alhdlaz*atbmi alhdlaz*bmicat/overlay href=25 30 LHREF=3; 
run;
quit;


/*7b:	Create bin means */
proc sort data=nonlinear;
by bmicat;
run;

proc means data=nonlinear;
by bmicat;
var alhdlaz;
output out=b mean=m;
run;

data nonlinear;
set nonlinear;
if bmicat=22 then binmean= 59.5391003;
if bmicat=27.5 then binmean= 51.8375000;
if bmicat=33 then binmean= 48.0125000;
run;


symbol1 color=black			
        interpol=none		
		value=dot;
symbol2 color=blue
		interpol=stepcj 
		value=star;
proc gplot data=nonlinear;
plot alhdlaz*atbmi binmean*bmicat/overlay href=25 30 LHREF=3;
run;
quit;





/**************************************************************************************/
/*BONUS EX -penalized bspline - default is DEGREE=3, 100 evenly spaced knots, three evenly spaced exterior knots on each side of the data.*/
/**************************************************************************************/

proc transreg data = nonlinear ; 
model identity(alhdlaz) = pbspline(atbmi /degree=3 nknots=100 aic)/ SS2; 
output out=pb PREDICTED;
run;

symbol1 color=black			
        interpol=none		
		value=dot;
symbol2 color=blue
		interpol=join width =2;

proc gplot data=pb;
plot (alhdlaz Palhdlaz )*atbmi/ overlay; 	
run; quit;

/* However, fit is not very good in this example due to  boundary constraints of SAS defaults 
-> check help files if and how you can change this */
